# This transaction shows how funds can be transferred from one account into multiple accounts all 
# in a single transaction. In this example, imagine that we would like to send XRD to three accounts
# in the following quantities:
# Account A: 150 XRD
# Account B: 130 XRD
# Account C: 50 XRD
# Which is a total of 330 XRD. 

# ==================================================================================================
# WARNING: If you will be submitting this transaction through the Babylon wallet then you MUST
# remove the "lock_fee" instruction that you see below. Otherwise your transaction will fail. If all 
# that you are using is resim then you can safely ignore this warning.
# ==================================================================================================
CALL_METHOD 
    Address("account_sim1cyvgx33089ukm2pl97pv4max0x40ruvfy4lt60yvya744cve475w0q") 
    "lock_fee"
    Decimal("10");

# Withdrawing 330 XRD from the account component
CALL_METHOD 
    Address("account_sim1cyvgx33089ukm2pl97pv4max0x40ruvfy4lt60yvya744cve475w0q") 
    "withdraw"
    Address("resource_sim1tknxxxxxxxxxradxrdxxxxxxxxx009923554798xxxxxxxxxakj8n3")
    Decimal("330");

# Taking 150 XRD from the worktop and depositing them into Account A
TAKE_FROM_WORKTOP
    Address("resource_sim1tknxxxxxxxxxradxrdxxxxxxxxx009923554798xxxxxxxxxakj8n3")
    Decimal("150")
    Bucket("account_a_bucket");
CALL_METHOD
    Address("account_sim1c8mulhl5yrk6hh4jsyldps5sdrp08r5v9wusupvzxgqvhlp4c4nwjz")
    "try_deposit_or_abort"
    Bucket("account_a_bucket");

# Taking 130 XRD from the worktop and depositing them into Account B
TAKE_FROM_WORKTOP
    Address("resource_sim1tknxxxxxxxxxradxrdxxxxxxxxx009923554798xxxxxxxxxakj8n3")
    Decimal("130")
    Bucket("account_b_bucket");
CALL_METHOD
    Address("account_sim1c8s2hass5g62ckwpv78y8ykdqljtetv4ve6etcz64gveykxznj36tr")
    "try_deposit_or_abort"
    Bucket("account_b_bucket");

# Taking 50 XRD from the worktop and depositing them into Account C
TAKE_FROM_WORKTOP
    Address("resource_sim1tknxxxxxxxxxradxrdxxxxxxxxx009923554798xxxxxxxxxakj8n3")
    Decimal("50")
    Bucket("account_c_bucket");
CALL_METHOD
    Address("account_sim1c8ct6jdcwqrg3gzskyxuy0z933fe55fyjz6p56730r95ulzwl3ppva")
    "try_deposit_or_abort"
    Bucket("account_c_bucket");