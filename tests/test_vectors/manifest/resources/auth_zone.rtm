## NOTE: This manifest is not intended to make cohesive sense
## - it is more intended to demonstrate various authzone-related commands 

# Withdraw XRD from account
CALL_METHOD Address("${account_address}") "withdraw" Address("${xrd_resource_address}") Decimal("5.0");

# Create a proof from bucket, clone it and drop both
TAKE_FROM_WORKTOP Address("${xrd_resource_address}") Bucket("some_xrd");
CREATE_PROOF_FROM_BUCKET Bucket("some_xrd") Proof("proof1");
CLONE_PROOF Proof("proof1") Proof("proof2");
DROP_PROOF Proof("proof1");
DROP_PROOF Proof("proof2");

# Create a proof from account and drop it
CALL_METHOD Address("${account_address}") "create_proof_by_amount" Address("${resource_address}") Decimal("5.0");
POP_FROM_AUTH_ZONE Proof("proof3");
DROP_PROOF Proof("proof3");

# Compose proofs
CALL_METHOD Address("${account_address}") "create_proof_by_amount" Address("${resource_address}") Decimal("5.0");
CREATE_PROOF_FROM_AUTH_ZONE Address("${resource_address}") Proof("Proof Name");
CREATE_PROOF_FROM_AUTH_ZONE_BY_AMOUNT Decimal("1") Address("${resource_address}") Proof("proof4");
CREATE_PROOF_FROM_AUTH_ZONE_BY_IDS Array<NonFungibleLocalId>(NonFungibleLocalId("#123#")) Address("${non_fungible_resource_address}") Proof("proof5");
CLEAR_AUTH_ZONE;

# Drop all virtual proofs in the auth zone.
CLEAR_SIGNATURE_PROOFS;

# Drop all proofs, and move resources to account
DROP_ALL_PROOFS;
CALL_METHOD Address("${account_address}") "deposit_batch" Expression("ENTIRE_WORKTOP");
